ext.javaVersion = 1.7
ext.kafkaVersion = '0.10.0.0'

project.version = ext.kafkaVersion


task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

allprojects {

    apply plugin: 'idea'

    plugins.withType(JavaPlugin).whenPluginAdded {

        version = rootProject.version
        sourceCompatibility = rootProject.javaVersion
        libsDirName = 'lib'

        if (!hasProperty('scalaVersion')) {
            ext.scalaVersion = '2.11.8'
        }

        if (scalaVersion.startsWith('2.10')) {
            ext.baseScalaVersion = '2.10'
        } else if (scalaVersion.startsWith('2.11')) {
            ext.baseScalaVersion = '2.11'
        } else {
            ext.baseScalaVersion = scalaVersion
        }

        configurations {
            provided
            testCompile.extendsFrom provided
        }

        repositories {
            mavenLocal()
            mavenCentral()
        }

        dependencies {
            compile group: 'org.slf4j', name:'slf4j-api', version:'1.7.6'
            compile group: 'org.slf4j', name:'slf4j-log4j12', version:'1.7.6'
            testCompile group: 'junit', name: 'junit', version: '4.+'
        }

        sourceSets {
            main {
                compileClasspath += [configurations.provided]
            }
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.provided]
            }
        }

    }
}

